:name: Multiple Machines emulation
:description: M1 and M2 connected via UART Hub

# Replace to your path
$add_on_repl?=$CWD/add_on.repl
$bin_path?=$CWD/../build/mos-renode.elf

$name1?="M1"
$name2?="M2"

# Create a symmetrical connection from UARTHub
emulation CreateUARTHub "uart_hub"

# ---------------- Create Machine 1 ----------------
mach create $name1
machine LoadPlatformDescription @platforms/boards/stm32f4_discovery-kit.repl
machine LoadPlatformDescription $add_on_repl
gpioPortA.UserButton Press # Give id=1

sysbus.nvic Frequency 160000000
macro reset
"""
    sysbus LoadELF $bin_path
"""
runMacro $reset
logLevel -1 gpioPortA.myLED

# Host Communication (for Shell)
emulation CreateServerSocketTerminal 3333 "uart-con1" false
connector Connect sysbus.usart2 uart-con1

# Inter-Communication (for M2M)
connector Connect sysbus.usart3 uart_hub

# ---------------- Create Machine 2 ----------------
mach create $name2
machine LoadPlatformDescription @platforms/boards/stm32f4_discovery-kit.repl
machine LoadPlatformDescription $add_on_repl
sysbus.nvic Frequency 160000000
macro reset
"""
    sysbus LoadELF $bin_path
"""
runMacro $reset
logLevel -1 gpioPortA.myLED

# Host Communication (for Shell)
emulation CreateServerSocketTerminal 3334 "uart-con2" false
connector Connect sysbus.usart2 uart-con2

# Inter-Communication (for M2M)
connector Connect sysbus.usart3 uart_hub